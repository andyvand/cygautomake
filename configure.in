# Process this file with autoconf to produce a configure script.

# Copyright 1995, 1996, 1997, 1998, 1999, 2000, 2001 Free Software
# Foundation, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
# 02111-1307, USA.

AC_PREREQ(2.52)
AC_INIT([GNU Automake], [1.5c], [bug-automake@gnu.org])

AC_CONFIG_SRCDIR(automake.in)
AC_CONFIG_AUX_DIR(lib)

AM_INIT_AUTOMAKE(automake, 1.5c)
AM_AUTOMAKE_OPTIONS([1.5 dist-bzip2])

ACLOCAL="`pwd`/aclocal --acdir=m4"
# $AUTOMAKE is always run after a `cd $top_srcdir', hence `.' is really
# what we want for perllibdir and libdir.
AUTOMAKE="perllibdir=./lib `pwd`/automake --libdir=lib"

AC_PATH_PROG(PERL, perl)
if test -z "$PERL"; then
   AC_MSG_ERROR([perl not found])
fi
$PERL -e 'require 5.005;' || {
   AC_MSG_ERROR([perl 5.005 or better is required])
}

echo 'AC''_PREREQ(2.52)' > conftest.ac
AM_RUN_LOG([eval $AUTOCONF -o /dev/null conftest.ac]) || {
   AC_MSG_ERROR([Autoconf 2.52 or better is required])
}
rm conftest.ac

AC_CONFIG_FILES([automake], [chmod +x automake])
AC_CONFIG_FILES([aclocal], [chmod +x aclocal])

AC_OUTPUT([Makefile
           lib/Makefile
           lib/Automake/Makefile
	   lib/am/Makefile
           m4/Makefile
           tests/Makefile])
